import net.darkhax.curseforgegradle.TaskPublishCurseForge
import net.darkhax.curseforgegradle.UploadArtifact

plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

version = "${minecraft_version}-${mod_version}"
group = maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url = "https://jitpack.io" }
}

loom {
    splitEnvironmentSourceSets()

	mods {
		"redirector" {
			sourceSet sourceSets.main
		}
	}

}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    implementation "net.minecraft:launchwrapper:1.12"
	//modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release.set(17)
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

String[] supportedGameVersions = new String[]{"${minecraft_version}"}
String currentChangeLog = file("CHANGELOG.md").getText("UTF-8");

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${archives_base_name}"
    versionNumber = "${mod_version}"
    uploadFile = remapJar
    gameVersions.addAll(supportedGameVersions)
    loaders = ["fabric", "quilt"]
    versionType = "beta"
    additionalFiles = [sourcesJar]
    changelog.set(currentChangeLog)
}

tasks.register("publishCurseForge", TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    disableVersionDetection()

    UploadArtifact mainFile = upload(cf_project_id, remapJar)
    mainFile.displayName = "${archives_base_name}-${version}"
    mainFile.releaseType = "beta"
    mainFile.changelog = currentChangeLog
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Fabric", "Quilt")
    mainFile.addJavaVersion("Java 17")
    mainFile.addGameVersion(supportedGameVersions)

    UploadArtifact sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    sourcesFile.changelog = currentChangeLog
    sourcesFile.changelogType = "markdown"
}